'0.979999
'// ^^ Ignore this version line, but DO NOT remove/modify it


'// The StealthBot Plugin System - StealthBot 2.7 Development Version
'//    by Swent
'//      Last modified 11:54 PM 3/11/2008

'// The StealthBot Plugin System allows StealthBot to load, run, and update plugins written in VBScript
'//    This code remains external to allow for automatic updates, and for viewing/tweaking by advanced scriptors.
'//        ** DO NOT change anything in this file unless you know what you're doing -- improper changes can crash the bot **

'// This file will be updated periodically to provide new plugin features and capabilities between StealthBot versions.


'// TODO BEFORE PUBLIC RELEASE
'// 	* Change version check to 2.7


'//**********************************//
'//  CHANGE LOG                      //
'//**********************************//
 
'// The full change log can be found here: http://stealthbot.net/p/plugs/changelog

'// ver .98 (BETA)

'//   [MODIFICATIONS]
'//   *Integrated portions of the Plugin System into the StealthBot internals to improve functionality.
'//   *Modified the Plugin System to make it compatiable with StealthBot v2.7
'//      -You can now use the FirstRun, MessageSent, Shutdown, LoggedOff, and the 9 new clan-related events in your plugins (see SSC)
'//      -Added the new Event arguements: Banned (UserJoins) and StatUpdate (UserInChannel) - see the SSC
'//         ~These will be automatically added to any UserJoins/UserInChannel events found missing them (Thanks Draco)
'//      -For compatibility reasons there is now a version check. You must have SB v2.7 or higher to use the Plugin System.
'//   *Modified the external .plugins response with an enabled/disabled indicator
'//      -Prefixes of disabled plugins are inside parenthesis
'//   *The phelp command can now be used externally (it will only display commands): .phelp <prefix>
'//   *Modified /plugins and /updates outputs -- now extremely sexy thanks to the new capabilites of AddChat (Thanks Jack)
'//   *Made the prefix validity check more thorough (now checks for repeats, invalid chars, and disallowed prefixes)
'//   *Revamped the settings system. Plugin settings are now located in settings.ini
'//      -Removed the /lset command as it is no longer neccessary
'//      -Changes to settings.ini now take effect after immediately after saving the file (You do not need to type "/lset" or hit Reload Script)
'//   *Opened up the updates, getplugin and delplugin commands to external usage by Bot Owner only (Thanks RoNiN)
'//      -In the external updates output, only prefixes are displayed. Key: New, [Updated], (Installed)
'//   *Increased plugin load efficiency -- load time should be noticeably decreased (Thanks ZergMasterI)
'//   *Replaced all Plugin System usaged of vbBlue with vbLightBlue, which is much easier on the eyes
'//   *Plugin and Plugin System update checks now only take place once after connection
'//   *Improved error handling during plugin load. Loading errors will now be more descriptive (Thanks ZergMaster)
'//      -Plugins with errors on load will be disabled automatically (to avoid the constant error messages on every event)
'//   *Improved plugin error handling while plugins are being loaded (Thanks ZergMasterI)
'//      -Errors recieved during plugin loading now show correct offending lines and line/column numbers, and the erroring plugin will be blocked
'//         ~"Blocked" is essentially the same as disabled -- except it's temporary. The blocked status disappears on reload (if error is fixed)
'//      -All other errors recieved will show correct line and column numbers as well if debug mode is on
'//   *Added the option to include a plugin description in the help lines. Include it on the first line (name:author:description)
'//   *The "/<prefix> off" command now triggers a call to the plugin's Close event (Thanks Ribose)

'//   [NEW FEATURES]
'//   *Added multiple command messages. As in StealthBot, you can now type: ".crs off; ss on; pelt me" etc.
'//      -This will work for Plugin System commands as well as all of your plugins' commands (No StealthBot commands)
'//      -This option can be disabled by setting the new "multipleCmdMsgs" plugin system setting to False
'//   *Added the option to back up plugins during updates by adding the "backup" setting for all plugins
'//      -Set to False by default. If changed to True, that plugin will be backed up when you download updates.
'//      -Alternatively, you can set the global "backup" PS setting to True, and it will apply to all plugins.
'//      -When True, the global "backup" PS setting overrides the individual "backup" plugin settings
'//   *Added settings that allow you to set access requirements for external Plugin System commands:
'//      -"prefix_cmd_access" (default 100), "plugins_cmd_access" (default 20), and "phelp_cmd_access" (default 20)
'//   *Added double-slash Plugin System commands. Inside bot type "//command" to have the response displayed publicly.
'//      -This will work for all Plugin System commands except /updates and /psnews
'//   *Added Plugin System help in /phelp command -- displays command names and explanations (Thanks raylu)
'//   *Added support for colons on the 4th phelp line (important notes) -- use "\c" for colon (Thanks ZergMasterI)
'//   *Added some helpful links at the completion of the installation (Thanks Snap)
'//   *Added StealthBot menu support for the Plugin System
'//      -In the new Plugins menu you'll find menus for each plugin
'//      -In these menus you can change enabled, nvn, and backup statuses, as well items for help and opening the plugin file
'//      -There is also a main help menu which provides links to revelant forums/threads.
'//      -5 subs/functions have been added to help you add and work with menu items for your plugin. See the SSC.
'//      -You can disable plugin menu creation by setting the menusDisabled PS setting to True (Thanks ZergMasterI)
'//   *Implemented #define's Plugin Creator. Can be executed from the Plugin System menu (Thanks #define)
'//   *Implemented plugin statistics system by Snap. See http://snapnjacks.com/stats.php
'//   *Added the /exec command to the Plugin System due to its common usage. Only works from inside the bot.

'//   [NEW COMMANDS]
'//    /delplugin <prefix>
'//      -Deletes the specified plugin from your plugins folder. A backup is created automatically.
'//      -To delete a plugin forever (no backup), add a parameter of "1". Example: /delplugin crs 1
'//    /psnews
'//      -Displays Plugin System news
'//    /pedit <prefix>
'//      -Opens the specified plugin for editing. This command has a few alternate functions:
'//         ~You can use a filename (ex: pmPeltMe.plug) in place of <prefix>, as long as it's in the plugins folder
'//         ~You can open your settings.ini file by omitting the <prefix> parameter
'//         ~You can open your plugins folder by using "plugins" for <prefix> (Thanks Jack)
'//    /exec <code>
'//      -Executes a line of VBScript code. Can only be used from inside the bot with "/" as the trigger.

'//   [BUG FIXES]
'//   *Fixed a bug in the debug command...lol (Thanks Jack)
'//   *Fixed minor error with the dsp sub -- it no longer attempts to split messages that have no spaces (Thanks Jack)
'//   *Fixed a problem with the plugin timer error handler -- sometimes it displayed the wrong file path
'//   *Fixed an error with the LoadSettings sub when a setting value contained "=" (Thanks Jack)
'//   *Fixed a minor bug with the .prefix on/off command (Thanks ZergMasterI)
'//   *Fixed a minor problem with the error handler related to "/prefix on" commands
'//   *Fixed a major changing variable issue (due to VBS's suckiness) by adding the Echo function (Thanks ZergMasterI)
'//   *Fixed a problem with the plugin converter in the case of a file already being partially converted (some/all events already prefixed)
'//      -Existing prepends found on event lines will be replaced with the correct prefix or ignored if it's already correct
'//   *Fixed a plugin converter error for users with certain language settings (Thanks fagju)


 
'//**********************************//
'//  THE STEALTHBOT PLUGIN SYSTEM    //
'//**********************************//


Const PS_TITLE = "The StealthBot Plugin System"
Const PS_UPDATE_URL = "http://www.stealthbot.net/p/plugs/"
Const PS_SBNET_URL = "http://www.stealthbot.net/board/index.php?"
Const PS_ALLOW_UI = True
Const PS_SET_PATH = "plugins\settings.ini"
Const PS_PREFIX_DISALLOW = "|ps|script|protect|phrasebans|ipbans|idle|koy|plugban|chpw|ib|efp|floodmode|debug"
Const PS_COLORS = "65280,39423"
Const PS_PC_VER = 2.4
Const PS_SM_VER = 1.1

Public psPrefixes       '// holds prefixes of loaded plugins
Public psTitles         '// holds plugin titles
Public psUpdateCounter  '// holds seconds since last updates check
Public psLoaded         '// holds True if the Plugin System has finished loading
Public psFirstRun       '// holds True if it's the first script load (so at bot opening)
Public psArgsAdded      '// holds True if missing plugin event arguments were added during load
Public psNewContent     '// holds modified plugin content after missing event arguments are added
Public psEventNames     '// holds all possible plugin event names
Public psD2             '// holds "*" If bot is using D2
 
'// Hold RGB values for custom vbColors (use with AddChat)
Public vbGrey, vbBrown, vbOrange, vbPink, vbLightBlue

'// Hold RGB values that make up the color scheme
Public psColor1, psColor2

Set psSC = GetScriptControl()
Set psFSO = CreateObject("Scripting.FileSystemObject")
Set psWshShell = CreateObject("WScript.Shell")
Set psAppShell = CreateObject("Shell.Application")
Set psLoadError = CreateObject("Scripting.Dictionary")     '// holds load error status (errored or not)
Set psCodeLoaded = CreateObject("Scripting.Dictionary")    '// holds status of plugin code (loaded or not)
Set psPlugins = CreateObject("Scripting.Dictionary")       '// holds plugin file paths
Set psVersions = CreateObject("Scripting.Dictionary")      '// holds plugin versions
Set psHelp = CreateObject("Scripting.Dictionary")          '// holds plugin help output
Set psEvents = CreateObject("Scripting.Dictionary")        '// holds event names found in each plugin





'// [[[ PLUGIN LOADING ]]]


Sub psLoadPlugins()

   If Not psLatestSB() Then Exit Sub
   On Error Resume Next

   '// Create an array of all existing StealthBot events
   psEventNames = Split("load|firstrun|serverinfo|servererror|usertalk|useremote|whisperfromuser|userjoins|userleaves|" & _
                        "flagupdate|loggedon|loggedoff|userinchannel|channeljoin|channellist|pressedenter|keyreturn|" & _
                        "messagesent|claninfo|clanmemberlist|clanmemberupdate|clanmotd|clanmemberleaves|" & _
                        "botremovedfromclan|botclanrankchanged|botjoinedclan|botclaninfo|close|shutdown", "|")

   '// Get list of allowed plugin file extensions
   extensions = LCase(GetSetting("ps", "allowedExtensions")) & ","
 
   '// Check each file in the plugins folder to see if it should be loaded
   For Each File In psFSO.GetFolder(BotPath & "plugins").Files

      Set curFile = psFSO.GetFile(File)
      If curFile.Size > 0 Then
 
         '// Check if the current file has one of the allowed plugin file extensions (plug or txt by default)
         If Instr(extensions, psFSO.GetExtensionName(File) & ",") Then

            strPlugin = psGetTextStream(File)
            arrPlugin = Split(strPlugin, vbCrLf)
            curPrefix = Mid(Trim(LCase(arrPlugin(0))), 2)
            curVersion = Mid(Trim(arrPlugin(1)), 2)
 
            '// Check if the file contents are in plugin form
            If Left(arrPlugin(0), 1) = "'" And Instr(curPrefix, " ") = 0 And UBound(arrPlugin) > 1 Then

               '// Check prefix and version validity
               pcErr = psPrefixCheck(curPrefix)
               If Len(pcErr) > 0 Then
                  AddChat vbRed, "Did not load plugin in file ", vbWhite, psFSO.GetFileName(File), vbRed, ". " & pcErr & "."
               ElseIf psGetNumericVersion(curVersion) < 0 Then
                  AddChat vbRed, "Did not load plugin in file ", vbWhite, psFSO.GetFileName(File), vbRed, ". Its version """ & curVersion & """ is invalid."
               Else      

                  '// Everything checks out, load this plugin!
                  psLoadPlugin curPrefix, curVersion, File, strPlugin, arrPlugin
                  If psPluginEnabled(curPrefix) Then enCount = enCount + 1
               End If
               
            Else

               '// If this file contains a StealthBot VBScript, convert it to plugin form
               If UBound(Filter(arrPlugin, "event_", 1, 1)) > -1 Then psConvertToPlugin File, arrPlugin
            End If
         End If
      End If
   Next
 
   AddChat psColor1, "Loaded " & psPlugins.Count & " plugins (" & CInt(enCount) & " enabled). Type ", psColor2, "/plug" & _
                     "ins", psColor1, " to view them. Type ", psColor2, "/phelp", psColor1, " for help using them."
   AddChat psColor1, "Type ", psColor2, "/updates", psColor1, " in the bot to view updates and new plugins that are available for download."

   If BotVars.NoSupportMultiCharTrigger Then
      AddChat vbYellow, "Plugins have been loaded that do not support multi-character triggers. Backwards compatibility mode has been enabled."
   End If

   psPrefixes = psPlugins.Keys
   psLoaded = True
End Sub

 
Sub psLoadPlugin(Prefix, Version, Path, Content, Lines)

   '// Get the names of all events that exist in this plugin
   psArgsAdded = False
   psEvents.Item(Prefix) = "|"
   For i = 0 to UBound(Lines)
      curLine = Trim(LCase(Lines(i)))

      If Instr(curLine, "event_") > 0 Then

         For j = 0 to UBound(psEventNames)
            curEventLine = "sub " & Prefix & "_event_" & psEventNames(j)
            If Left(curLine, Len(curEventLine)) = curEventLine Then
               psEvents.Item(Prefix) = psEvents.Item(Prefix) & psEventNames(j) & "|"
            End If
         Next

         '// Check for a split line
         If Right(curLine, 1) = "_" And i < UBound(Lines) Then
           curLine = curLine & Trim(LCase(Lines(i + 1)))
           intLine = i + 1
         Else
           intLine = i
         End If
 
         '// If it's a UserJoins or UserInChannel event, make sure the new arguments exist
         If Instr(curLine, "userjoins") > 0 And Instr(curLine, "banned") = 0 Then
            psAddArg Prefix, intLine, "UserJoins", "OriginalStatString", "Banned", Lines, Path
         ElseIf Instr(curLine, "userinchannel") > 0 And Instr(curLine, "statupdate") = 0 Then
            psAddArg Prefix, intLine, "UserInChannel", "Product", "StatUpdate", Lines, Path
         End If
      End If
   Next

   '// Store the plugin's path and version
   psPlugins.Item(Prefix) = Path
   psVersions.Item(Prefix) = Version

   '// Get any help information provided by this plugin
   If IsArray(Lines) Then
      If UBound(Lines) > 4 Then 
         ReDim arrHelp(3)
         For i = 2 to 5
            If Left(Lines(i), 2) = "'&" Then
               arrHelp(i - 2) = Trim(Mid(Lines(i), 3))
               If i = 2 Then strTitle = Split(arrHelp(i - 2), ":")(0)
            End If               
         Next
         psHelp.Add Prefix, arrHelp
      End If
   End If

   '// Store the title for use in the plugins menu
   If Len(strTitle) = 0 Then strTitle = Prefix
   psTitles = psTitles & strTitle & "|||"

   '// Create the default settings for this plugin
   SetSetting Prefix, "enabled", True, "", False
   SetSetting Prefix, "menu_display", True, "", False
   SetSetting Prefix, "nvn", True, "", False
   SetSetting Prefix, "backup", False, "", False

   '// Load this plugin's code
   psLoadCode Prefix, Path, Content
End Sub


Sub psLoadCode(Prefix, Path, Content)

   '// Load the plugin's code (unless it's disabled)
   On Error Resume Next
   boolEnabled = GetSetting(Prefix, "enabled")
   If (Len(boolEnabled) = 0 Or GetSetting(Prefix, "enabled")) Then
      If psArgsAdded Then
          Content = psNewContent
      End If
      
      '// Check if plugin supports multiple-character triggers
      If Not BotVars.NoSupportMultiCharTrigger Then
         If ((InStr(1, Content, ", 1) = BotVars.Trigger", vbTextCompare) <> 0) Or _
             (InStr(1, Content, ", 1) <> BotVars.Trigger", vbTextCompare) <> 0)) Then
  
            '// Not supported -- enable backwards compatibility mode
            BotVars.NoSupportMultiCharTrigger = True
         End If
      End If

      psSC.AddCode Content

      psCodeLoaded.Item(Prefix) = True
   Else
      psCodeLoaded.Item(Prefix) = False
   End If

   '// Handle loading errors
   If Err.Number <> 0 Then
      'LoadError.Item(Prefix) = True
      AddChat vbRed, "Plugin loading error '" & Err.Number & "': " & Err.Description
      AddChat vbRed, "Offending file: >> " & Path
      AddChat vbYellow, "Your " & Prefix & " plugin has been temporarily blocked due to a loading error."
      Err.Clear
   Else
      psLoadError.Item(Prefix) = False
   End If
End Sub


Sub psAddArg(Prefix, Index, EventName, LastArg, NewArg, Lines, Path)

   For i = 0 to UBound(Lines)
     If i = Index Then Lines(i) = Replace(Lines(i), LCase(LastArg), LastArg & ", " & NewArg, 1, -1, 1)
   Next

   psNewContent = Join(Lines, vbCrLf)
   Set pFile = psFSO.OpenTextFile(Path, 2, 1)
   pFile.WriteLine psNewContent
   pFile.Close
   psArgsAdded = True
End Sub





'/// [[[ PLUGIN SYSTEM COMMANDS ]]]
 

Sub psProcessCmd(Message, Username, DisplayID)

   '// Has the Plugin System loaded completely?
   If Not psLoaded Then Exit Sub

   '// Is it a command?
   If (Not (Left(Message, 1) = "/" Or (Left(Message, Len(BotVars.Trigger)) = BotVars.Trigger And Username <> BotVars.Username))) Then Exit Sub

   If (Left(Message, 2) = "//") Then
      DisplayID = 1
      Message = Mid(Message, 3)
   ElseIf (Left(Message, 1) = "/") Then
      Message = Mid(Message, 2)
   ElseIf (Left(Message, Len(BotVars.Trigger)) = BotVars.Trigger) Then
      Message = Mid(Message, Len(BotVars.Trigger) + 1)
   End If

   '// Get the command name and arguements
   If Trim(Message) = vbNullString Or Len(Message) < 2 Then Exit Sub
   cmd = Split(Trim(LCase(Message & " "))): strCmdName = cmd(0)
 
   '// Is it a .[prefix] on/off command?
   If UBound(cmd) = 1 Then
      If (cmd(1) = "on" Or cmd(1) = "off") And strCmdName <> "debug" Then
         cmdPrefix = strCmdName: strCmdName = "prefix"
 
         '// Make sure it's a valid prefix
         If Not (psPlugins.Exists(cmdPrefix) Or cmdPrefix = "ps") Then Exit Sub
      End If
   Else
      If strCmdName = "prefix" Then Exit Sub
   End If

   '// Have they entered a plugin system command?
   If Username = BotVars.Username Then
 
      '// Is this a valid internal plugin system command?
      Select Case strCmdName
         Case "prefix", "updates", "getplugin", "debug", "lset", "plugins", "nvn", "ext", "phelp", "delplugin", "psnews", "pedit"
            VetoThisMessage
         Case Else: Exit Sub
      End Select
   Else
      GetDBEntry Username, myAccess, myFlags
 
      '// Is this a valid external plugin system command and is there sufficient access?
      Select Case strCmdName
         Case "prefix", "plugins", "phelp"
            If myAccess < CInt(GetSetting("ps", strCmdName & "_cmd_access")) Then Exit Sub
         Case "updates", "getplugin", "delplugin"
            If myAccess < 1000 Then Exit Sub '// Bot Owner only
         Case Else: Exit Sub
      End Select
   End If

   '// Call the appropriate command sub
   Select Case strCmdName
      Case "prefix":    ps_prefix_cmd cmd, cmdPrefix, Username, DisplayID
      Case "updates":   ps_updates_cmd cmd, Username, DisplayID
      Case "getplugin": ps_getplugin_cmd cmd, Username, DisplayID
      Case "debug":     ps_debug_cmd cmd, Username, DisplayID
      Case "plugins":   ps_plugins_cmd cmd, Username, DisplayID
      Case "nvn":       ps_nvn_cmd cmd, Username, DisplayID
      Case "phelp":     ps_phelp_cmd cmd, Username, DisplayID
      Case "delplugin": ps_delplugin_cmd cmd, Username, DisplayID
      Case "pedit":     ps_pedit_cmd cmd, Username, DisplayID
      Case "psnews":    ps_psnews_cmd
      Case "lset"
         AddChat psColor2, "Command removed -- changes to settings.ini take effect after saving the file (using /lset or Reload Script not necessary)."
      Case "ext"
         AddChat psColor2, "The ext command has been removed. Please change the extensions manually in your settings.ini file."
   End Select
End Sub
       

'// Enable/disables the specified plugin, or all plugins
Sub ps_prefix_cmd(cmd, Prefix, Username, DisplayID)
 
   arrOnOff = psOnOff(cmd(1))
   boolLastSetting = GetSetting(Prefix, "enabled")
   SetSetting Prefix, "enabled", arrOnOff(0), "", True

   If Prefix = "ps" Then
      If Not arrOnOff(0) Then
         dsp DisplayID, "Plugins have been globally disabled.", Username, vbRed
      Else
         dsp DisplayID, "Plugins are no longer globally disabled.", Username, vbGreen

         '// Reversing global disablement?
	 If arrOnOff(0) And Not boolLastSetting Then
            For i = 0 to UBound(psPrefixes)
               If GetSetting(psPrefixes(i), "enabled") Then psLoadOrClose psPrefixes(i), "Load"
            Next
         End If
      End If
   Else
      dsp DisplayID, "The " & Prefix & " plugin has been " & arrOnOff(1) & ".", Username, arrOnOff(2)

      '// Enabling an unloaded plugin?
      If arrOnOff(0) And Not psCodeLoaded(Prefix) Then
         psLoadCode Prefix, psPlugins.Item(Prefix), psGetTextStream(psPlugins.Item(Prefix))
         psLoadOrClose Prefix, "Load"

      '// Disabling a plugin?
      ElseIf Not arrOnOff(0) Then
         psLoadOrClose Prefix, "Close"
      End If
   End If

   '// Toggle enabled checkmark in plugin menu
   If Prefix = "ps" Then boolEnabled = Not arrOnOff(0) Else boolEnabled = arrOnOff(0) End If
   SetMenuItemCheck Prefix, "Enabled", boolEnabled
End Sub


'// Displays new plugins and updates available for download - Thanks FiftyToo!
Sub ps_updates_cmd(cmd, Username, DisplayID)

   If scINet.StillExecuting Then
      AddChat vbRed, "Connection to plugins server is busy. Try again in a couple minutes.": Exit Sub
   End If

   Content = scInet.OpenURL(PS_UPDATE_URL & "update.php?action=getlist")
   If Instr(Content, "|||") = 0 Then
      Call psCannotConnect(): Exit Sub
   End If
   Lines = Split(Content, vbCrLf)
   strUpdates = "%4 Color Key: %1 New, %2Updated, %3Installed%4"
 
   For i = 0 to UBound(Lines)
      curLine = Split(Lines(i), "|||")
      curPrefix = LCase(curLine(1))
 
      '// Is this plugin not installed or is there a newer version available?
      If Not psPlugins.Exists(curPrefix) Then
         dispColor = "%1"
	 dlList = dlList & curPrefix & ", "
      ElseIf psGetNumericVersion(psVersions.Item(curPrefix)) < psGetNumericVersion(curLine(2)) Then
         dispColor = "%2"
         dlList = dlList & "[" & curPrefix & "], "
      Else
         dispColor = "%3"
	 dlList = dlList & "(" & curPrefix & "), "
      End If
 
      If Lines(i) <> vbNullString Then
         strUpdates = strUpdates & dispColor & "%4 [ " & curLine(0) & " ]%4 Plugin Version: " & curLine(2) & _
                      "%4 " & "Description: " & curLine(3) & "%4 " & "Prefix: " & curPrefix & "%4"
      End If
   Next
   If DisplayID < 4 Then
      dsp DisplayID, "Plugin Downloads: " & Left(dlList, Len(dlList) - 2), Username, 0
   Else

      If Not GetSetting("ps", "nvn") Then
         strUpdates = strUpdates & "%4" & """, 255, """ & "You aren't recieving notification of new versions for " & _
                                                             "any plugins. To reverse this type /nvn " & "ps" & " on"
      End If
      strUpdates = Replace(strUpdates, "%1", """, psColor1, """)
      strUpdates = Replace(strUpdates, "%2", """, vbCyan, """)
      strUpdates = Replace(strUpdates, "%3", """, psColor2, """)
      strUpdates = Replace(strUpdates, "%4", """ & vbCrLf & """)
      strUpdates = Replace(strUpdates, """"" &", "")
 
      AddChat psColor2, "ÿcbÿcuOfficial StealthBot Plugins and Updatesÿcuÿcbÿciÿci"
      Execute "AddChat vbWhite, """ & strUpdates & """"
      AddChat psColor1, "ÿcbTo download one of the plugins above, type /getplugin <prefix>"
   End If
End Sub


'// Downloads a plugin from the plugins server.
Sub ps_getplugin_cmd(cmd, Username, DisplayID)

   If UBound(cmd) > 0 Then
      Prefix = cmd(1)
   Else
      psProcessCmd "/updates", BotVars.Username, 4: Exit Sub
   End If

   If scINet.StillExecuting Then
      AddChat vbRed, "Connection to plugins server is busy. Try again in a couple minutes.": Exit Sub
   End If
  
   Content = scInet.OpenURL(PS_UPDATE_URL & "update.php?action=getscript&script=" & Prefix)
   If Content = "invalid" Then
     AddChat vbRed, "Invalid command: None of the plugins available for download have prefix """ & Prefix & """."
     AddChat vbRed, "Type /updates to view the prefixes of available plugins."
     Exit Sub
   ElseIf Instr(Content, "|") = 0 Then
     Call psCannotConnect()
     Exit Sub
   End If

   '// Do we need to back up an existing version before we download?
   If psPlugins.Exists(Prefix) Then
      If GetSetting(Prefix, "backup") Or GetSetting("ps", "backup") Then
        psBackUpFile psPlugins.Item(Prefix), psVersions.Item(Prefix)
      End If
   End If

   '// Get names of needed files
   cvars = Split(Content, "|")
   If Right(cvars(0),1) = "," Then
     cvars(0) = Left(cvars(0), Len(cvars(0)) - 1)
   End If
   Files = Split(cvars(0), ",")
   pluginFileName = Mid(Files(0), 20) & ".plug"      

   '// Download the plugin
   AddChat vbLightBlue, "Downloading File: " & pluginFileName
   SSC.PrintURLToFile "plugins\" & pluginFileName, PS_UPDATE_URL & Files(0)

   If Not psPlugins.Exists(Prefix) Then

      '// Download the additonal files (if any)
      If UBound(Files) > 0 Then AddChat vbWhite, "*** NOTE: If you get a Script Control pop-up, hit Continue. Hitting End will cause errors!"
      For i = 1 to UBound(Files)
        AddChat vbLightBlue, "Downloading File: " & Files(i)
	If Files(i) <> "" Then SSC.PrintURLToFile "plugins\" & Files(i), PS_UPDATE_URL & Files(i)
      Next

      If DisplayID = 4 Then trig = "/" Else trig = BotVars.Trigger End If
      dsp DisplayID, "The " & cvars(1) & " plugin installed successfully. For help, " & _
		     "type """ & trig & "phelp " & Prefix & """ inside your bot.", Username, psColor1
   Else
      dsp DisplayID, "The " & cvars(1) & " plugin was updated successfully.", Username, psColor1
   End If
   AddChat psColor2, "Reloading Plugins...": Call ReloadScript()
End Sub
 
 
'// Enables/disables debug mode
Sub ps_debug_cmd(cmd, Username, DisplayID)

   If UBound(cmd) = 1 Then
      If cmd(1) = "on" Or cmd(1) = "off" Then
         arrOnOff = psOnOff(cmd(1))
         dsp DisplayID, "Plugin debug mode has been " & arrOnOff(1) & ".", 0, arrOnOff(2)
         SetSetting "ps", "debugmode", arrOnOff(0), "", True
      End If
   End If
End Sub


'// Displays list of all plugins currently loaded
Sub ps_plugins_cmd(cmd, Username, DisplayID)
 
   arrPrefixes = psPlugins.Keys
   arrTitles = Split(psTitles, "|||")

   If DisplayID = 3 Or DisplayID = 1 Then
      For i = 0 to UBound(arrPrefixes)
         If Not psPluginEnabled(arrPrefixes(i)) Then
            arrPrefixes(i) = "(" & arrPrefixes(i) & ")"
         End If
      Next
      dsp DisplayID, "Loaded Plugins (" & psPlugins.Count & "): " & Join(arrPrefixes, ", "), Username, vbWhite
      Exit Sub
   End If

   For i = 0 to psPlugins.Count - 1
      If i Mod 2 = 0 Then altColor = psColor1 Else altColor = psColor2 End If
      If psPluginEnabled(arrPrefixes(i)) Then
         If psLoadError.Item(arrPrefixes(i)) Then
            status = "vbRed, "" (Blocked)"" & vbCrLf, "
         Else
            status = altColor & ", "" "" & vbCrLf, "
         End If
      Else
         status = "vbRed, "" (Disabled)"" & vbCrLf, "
      End If

      arrTitles = Split(psTitles, "|||")
      If Len(arrTitles(i)) > 0 Then 
	 titleLine = altColor & ", ""    Title:" & String(2, vbTab) & ""& arrTitles(i) & """ & vbCrLf, "
      Else
         titleLine = ""
      End If

      'If i = 0 Then strFont = Array("ÿcb ÿcb", "ÿcu ÿcu", "ÿci ÿci") Else strFont = Array("","","") End If
      plugVer = altColor & ", ""    Version:" & vbTab & ""& psVersions.Item(arrPrefixes(i)) & """ & vbCrLf, "
      fileLine = altColor & ", ""    Filename:" & vbTab & "" & psFSO.GetFileName(psPlugins.Item(arrPrefixes(i))) & """, "
      prefixLine = altColor & ", String(2, vbCrLf) & ""    Prefix:" & String(2, vbTab) & """, " & vbWhite & ", ""[ " & arrPrefixes(i)  & " ]"", "
      strPList = strPList & prefixLine & status & "vbCrLf & " & titleLine & plugVer & fileLine
   Next

   AddChat psColor1, "ÿcbÿcuLoaded Plugins(" & psPlugins.Count & ")ÿcuÿcbÿciÿci"
   Execute("AddChat " & Left(strPList, Len(strPList) - 2))
   If Not GetSetting("ps", "enabled") Then AddChat vbRed, "Plugins have been globally disabled. To reverse this type /ps on"
End Sub


'// Enables/disables new version notification for a specified plugin, or all plugins
Sub ps_nvn_cmd(cmd, Username, DisplayID)

   If UBound(cmd) = 2 Then
      If psPrefixExists(cmd(1)) Then
         If cmd(2) = "on" Or cmd(2) = "off" Then
            arrOnOff = psOnOff(cmd(2))
            SetSetting cmd(1), "nvn", arrOnOff(0), "", True
            If cmd(1) = "ps" Then
               If Not arrOnOff(0) Then
                  dsp DisplayID, "New Version Notification has been globally disabled.", 0, vbRed
               Else
                  dsp DisplayID, "New Version Notficiation is no longer globally disabled.", 0, vbGreen
               End If
            Else
               dsp DisplayID, "New Version Notification for your " & cmd(1) & " " & _
                              "plugin has been " & arrOnOff(1) & ".", 0, arrOnOff(2)
            End If

            '// Toggle nvn checkmark in plugin menu
            If cmd(1) = "ps" Then boolEnabled = Not arrOnOff(0) Else boolEnabled = arrOnOff(0) End If
            SetMenuItemCheck cmd(1), "New Version Notification", boolEnabled
         End If
      End If
   End If
End Sub

 
'// Displays help on a specified plugin
Sub ps_phelp_cmd(cmd, Username, DisplayID)

   '// Did user enter a prefix?
   If UBound(cmd) = 0 Then
      If DisplayID = 4 Then AddChat vbLightBlue, "Plugin Help System"
      dsp DisplayID, "Type /phelp <prefix> to get help on one of your loaded plugins.", Username, psColor2
      dsp DisplayID, "Loaded prefixes: " & Join(psPrefixes, ", "), Username, psColor1
      If DisplayID = 4 Then AddChat vbGrey, "For help with the Plugin System's commands and operation, type /phelp ps"
   Else
      '// Is this a valid prefix?   
      If Not psPrefixExists(cmd(1)) Then Exit Sub

      '// Did they enter ps as prefix?
      If cmd(1) = "ps" Then
        If DisplayID = 4 Then
           If Not scINet.StillExecuting Then
              AddChat psColor2, scINet.OpenURL(PS_UPDATE_URL & "pscmds.txt") & vbCrLf
           End If
           AddChat psColor1, "For additional information please visit the Plugin System thread:"
           AddChat vbWhite, "http://www.stealthbot.net/board/index.php?showtopic=6052"
        Else
           strCmds = "[prefix] on/off, " & BotVars.Trigger & "plugins, " & BotVars.Trigger & "phelp [prefix]"
           dsp DisplayID, "External Plugin System Commands: " & BotVars.Trigger & strCmds, Username, 0
           dsp DisplayID, "Additional info: http://www.stealthbot.net/board/index.php?showtopic=6052", Username, 0
        End If
        Exit Sub
      End If

      '// Is help available for this plugin?
      If Join(psHelp.Item(cmd(1)), "") = vbNullString Then
         dsp DisplayID, "Sorry, no help information is available for your " & cmd(1) & " plugin.", Username, vbLightBlue
         Exit Sub
      End If

      '// If an external command, display only the commands
      If DisplayID <> 4 Then
         commands = Split(psHelp.Item(cmd(1))(1), ":")
         dsp DisplayID, "Commands: " & BotVars.Trigger & Join(commands, ", " & BotVars.Trigger), Username, 0
         Exit Sub
      End If

      arrLine1 = Split(psHelp.Item(cmd(1))(0), ":")
      arrCmds = Split(psHelp.Item(cmd(1))(1), ":")
      topicNum = psHelp.Item(cmd(1))(2)
      arrNotes = Split(psHelp.Item(cmd(1))(3), ":")

      '// Display title, author, and description
      If UBound(arrLine1) = 0 Then strAuthor = "Unknown" Else strAuthor = arrLine1(1) End If
      AddChat vbLightBlue, "ÿcbÿcb" & arrLine1(0) & " by " & strAuthor
      If UBound(arrLine1) > 1 Then AddChat vbLightBlue, Space(6) & arrLine1(2)

      '// Display commands
      AddChat vbWhite, " ": AddChat psColor2, "Commands:"
      If Join(arrCmds, "") = vbNullString Then
         AddChat psColor2, "There are no commands for this plugin."
      Else
         For i = 0 to UBound(arrCmds): AddChat psColor2, Space(6) & BotVars.Trigger & arrCmds(i): Next
      End If

      '// Display important notes and forum link
      If Join(arrNotes, "") <> vbNullString Then
         AddChat vbWhite, " ": AddChat vbRed, "Other Important Notes:"
         For i = 0 to UBound(arrNotes): AddChat vbRed, Space(6) & "-" & Replace(arrNotes(i), "\c", ":"): Next
      End If
      If topicNum <> vbNullString Then
         url = "http://www.stealthbot.net/forumn/index.php?showtopic=" & topicNum
         AddChat vbWhite, " ": AddChat psColor1, "Visit " & url & " for additional information on this plugin."
      End If
   End If
End Sub


'// Deletes the specified plugin file
Sub ps_delplugin_cmd(cmd, Username, DisplayID)

   If UBound(cmd) > 0 Then
      If Not psPlugins.Exists(cmd(1)) Then
         AddChat vbRed, "Invalid command: None of your loaded plugins have prefix """ & cmd(1) & """."
         Exit Sub
      Else
         If Not psFSO.FileExists(psPlugins.Item(cmd(1))) Then
            AddChat psColor2, "That plugin has already been deleted."
         Else

            '// Back up the plugin file that's about to be deleted
            If UBound(cmd) > 1 Then
               If cmd(2) <> 1 Then bkup = True
            Else
               bkup = True
            End If 
            If bkup Then psBackUpFile psPlugins.Item(cmd(1)), psVersions.Item(cmd(1))

            '// Delete the file
            psFSO.DeleteFile(psPlugins.Item(cmd(1)))
            dsp DisplayID, "The " & cmd(1) & " plugin has been deleted.", Username, vbLightBlue
            AddChat psColor2, "Reloading Plugins...": Call ReloadScript()
         End If
      End If
   End If
End Sub
   

'// Opens the specified plugin file for editing.
Sub ps_pedit_cmd(cmd, Username, DisplayID)

   If UBound(cmd) = 0 Then
      path = BotPath() & PS_SET_PATH
   ElseIf cmd(1) = "plugins" Then
      path = BotPath() & cmd(1)
   Else
      If Instr(cmd(1), ".") Then
         If psFSO.FileExists(BotPath() & "plugins\" & cmd(1)) Then
            path = BotPath() & "plugins\" & cmd(1)
         Else
            AddChat vbRed, "Invalid command: The file """ & cmd(1) & """ doesn't exist in your plugins folder."
            Exit Sub
         End If
      Else
         If Not psPrefixExists(cmd(1)) Then Exit Sub

	 If cmd(1) = "ps" Then
            path = BotPath() & "PluginSystem.dat"
         Else
            path = psPlugins.Item(cmd(1))
         End If
      End If
   End If
   psAppShell.Open path
End Sub


'// Displays Plugin System news
Sub ps_psnews_cmd()

   If scINet.StillExecuting Then
      AddChat vbRed, "Connection to plugins server is busy. Try again in a couple minutes.": Exit Sub
   End If

   news = scINet.OpenURL(PS_UPDATE_URL & "news.php")
   If Instr(news, "Plugin System") = 0 Then Exit Sub
   arrNews = Split(news, vbCrLf)
   For i = 0 to UBound(arrNews)
      AddChat 13408512, arrNews(i)
   Next
End Sub





'// [[[ PLUGIN CONVERTER ]]]


Sub psConvertToPlugin(Path, Lines)

   '// Is the plugin converter disabled?
   If Not GetSetting("ps", "autoConvertToPlugin") Then Exit Sub

   strDlgTitle = PS_TITLE & " - Plugin Converter"
   fileName = Mid(Path, InstrRev(Path, "\") + 1)
   convertNow = MsgBox("A file in your plugins folder contains a script that isn't in plugin format: " & String(2, vbCrLf) & _
                        fileName & String(2, vbCrLf) & "Would you like to convert this script to plugin format " & _
                       "now?", vbYesNoCancel + vbQuestion + vbApplicationModal, strDlgTitle)

   If convertNow = vbYes Then
 
      '// Get the desired prefix for the converted plugin
      Do
         prefix = LCase(InputBox("Enter a prefix for this plugin.", strDlgTitle))
         askAgain = False
 
         '// Make sure they've entered something
         If Trim(prefix) = vbNullString Then
            MsgBox "You must enter a prefix.", vbExclamation + vbApplicationModal, strDlgTitle
            askAgain = True
         Else
            '// Make sure they've entered a valid prefix
            If psPlugins.Exists(prefix) Then
               MsgBox "The prefix """ & prefix & """ already exists.", vbExclamation + vbApplicationModal, strDlgTitle
               askAgain = True
            ElseIf Instr(prefix, " ") Then
               MsgBox "You can't have spaces in your prefix.", vbExclamation + vbApplicationModal, strDlgTitle
               askAgain = True
            ElseIf Len(psPrefixCheck(prefix)) > 0 Then
               MsgBox "Invalid prefix.", vbExclamation + vbApplicationModal, strDlgTitle
               askAgain = True
            End If
         End If
      Loop While askAgain

      '// Convert the code to plugin form
      For i = 0 to UBound(Lines)

         '// Convert event sub headers and event calls
         If Instr(LCase(Lines(i)), "event_") Then
	    If Instr(Replace(LCase(Lines(i)), " ", ""), "subevent") > 0 Then
              Lines(i) = Replace(Lines(i), "event_", prefix & "_event_", 1, -1, 1)
            Else
              Lines(i) = Replace(Lines(i), "  ", " ")
              Lines(i) = Replace(Lines(i), Split(Split(Lines(i))(1), "_")(0), prefix)
            End If
 
         '// Convert timer code
         ElseIf Instr(Replace(LCase(Lines(i)), " ", ""), "sctimer.interval=") Then
            interval = Trim(Mid(Lines(i), Instr(Lines(i), "=") + 1))
            Lines(i) = "TimerInterval """ & prefix & """,""" & prefix & "Timer""," & CLng(interval) \ 1000
         ElseIf Instr(Replace(LCase(Lines(i)), " ", ""), "sctimer.enabled=") Then
            bool = Mid(Lines(i), Instr(Lines(i), "=") + 1)
            Lines(i) = "TimerEnabled """ & prefix & """,""" & prefix & "Timer""," & bool
         ElseIf Instr(LCase(Lines(i)), "sctimer.enabled") Then
            convertedLine = "GetTimerEnabled(""" & prefix & """,""" & prefix & "Timer"")"
            Lines(i) = Replace(Lines(i), "sctimer.enabled", convertedLine, 1, -1, 1)
         ElseIf Left(LCase(Lines(i)), 19) = "sub sctimer_timer()" Then
            Lines(i) = "sub " & prefix & "_" & prefix & "Timer_Timer()"
         End If
      Next

      '// Write the prefix and version line, and the converted code
      Set pluginFile = psFSO.OpenTextFile(Path, 2, 1)
      With pluginFile
         .WriteLine "'" & prefix & vbCrLf & "'1.0" & vbCrLf
         .WriteLine Join(Lines, vbCrLf)
         .Close
      End With   
 
      '// Has a plugin with the same prefix already been loaded?
      If psPlugins.Exists(curPrefix) then
         AddChat vbRed, "A plugin file was not loaded, as it's prefix "" & curPrefix & "" already exists: " & fileName
      Else
         '// Load the converted plugin
         AddChat psColor2, "Successfully converted a script to plugin format with prefix """ & prefix & """ in file: " & fileName
         strPlugin = psGetTextStream(Path)
         psLoadPlugin prefix, "1.0", Path, strPlugin, Split(strPlugin, vbCrLf)
      End If
   End If
End Sub





'// [[[ PLUGIN UPDATES ]]]


Sub psAutoUpdate(boolMenuClick)

   boolFoundUpdate = False
   On Error Resume Next

   '// Check for plugin system updates
   If GetSetting("ps", "psnvn") or boolMenuClick Then
      If scINet.StillExecuting Then Exit Sub

      '// Successful connection with the plugins server? If not, stop update checks
      curPSVersion = scINet.OpenURL(PS_UPDATE_URL & "update.php?action=sctxtver")
      If Len(curPSVersion) = 0 Or Instr(curPSVersion, " ") > 0 Then
         If boolMenuClick Then Call psCannotConnect()
         Exit Sub
      End If

      '// Get local Plugin System version
      Set myPSFile = psFSO.OpenTextFile(BotPath() & "PluginSystem.dat", 1)
      myPSVersion = Mid(myPSFile.ReadLine(), 2)
      myPSFile.Close
 
      '// Is there are newer Plugin System version available?
      If Len(myPSVersion) > 0 Then
         If psGetNumericVersion(curPSVersion) > psGetNumericVersion(myPSVersion) Then
            boolFoundUpdate = True

            updatePS = MsgBox("An new version (v" & curPSVersion & ") is available for " & PS_TITLE & ". Automatically " & _
                              "update now (recommended)?" & String(2, vbCrLf) & "This notification can be disabled inside " & _
                              "your settings.ini file.", vbYesNoCancel + vbQuestion + vbApplicationModal, PS_TITLE & " - Updater")

            If updatePS = vbYes Then
                  AddChat vbLightBlue, "Downloading an update for " & PS_TITLE
                  psBackUpFile BotPath() & "PluginSystem.dat", myPSVersion
                  PrintURLToFile "PluginSystem.dat", PS_UPDATE_URL & "getplug.php?plugin=pluginsystem"
                  AddChat psColor1, "Your Plugin System has been updated."
                  AddChat psColor1, "For a list of changes in this version see: http://stealthbot.net/p/plugs/changelog"
                  AddChat psColor2, "Reloading Plugins...": Call ReloadScript()
            End If
         End If
      End If
   End If

   '// Check for plugin creator updates.
   strPCVer = GetSetting("#internal", "pcvernote")
   If Len(strPCVer) > 0 And Not strPCVer Then
      If scINet.StillExecuting Then Exit Sub

      curPCVersion = scINet.OpenURL("http://stealthbot.net/p/plugs/pcver.txt")
      dblPCVer = GetSetting("#internal", "pcver")
 
      If psFSO.FileExists("PluginCreator.exe") Then    
         If Len(dblPCVer) > 0 Then
            If psGetNumericVersion(curPCVersion) > psGetNumericVersion(dblPCVer) Then
               boolFoundUpdate = True
               psFSO.DeleteFile BotPath() & "PluginCreator.exe"
               AddChat vbGreen, "An update is available for your Plugin Creator. To download hit Plugins > Advanced > Plugin Creator"
               SetSetting "#internal", "pcvernote", True
            End If       
         End If
      End If
   End If

   '// Check for settings manager updates.
   strSMVer = GetSetting("#internal", "smvernote")
   If Len(strSMVer) > 0 And Not strSMVer Then
      If scINet.StillExecuting Then Exit Sub

      curSMVersion = scINet.OpenURL("http://stealthbot.net/p/plugs/smver.txt")
      dblSMVer = GetSetting("#internal", "smver")

      If psFSO.FileExists("PluginSettings.exe") Then
         If Len(dblSMVer) > 0 Then
            If psGetNumericVersion(curSMVersion) > psGetNumericVersion(dblSMVer) Then
               boolFoundUpdate = True
               psFSO.DeleteFile "PluginSettings.exe"
               AddChat vbGreen, "An update is available for your Plugin Settings Manager. To download hit Plugins > Settings Manager"
               SetSetting "#internal", "smvernote", True
            End If
         End If
      End If
   End If

   '// Check for plugin updates
   If GetSetting("ps", "nvn") Or boolMenuClick Then
      If scINet.StillExecuting Then Exit Sub

      Content = scInet.OpenURL(PS_UPDATE_URL & "update.php?action=getlist")
      Lines = Split(Content, vbCrLf)
      For i = 0 to UBound(Lines)
         curLine = Split(Lines(i), "|||")

         '// Is this plugin installed?
         If psPlugins.Exists(curLine(1)) Then

            '// Is the available version greater than the local version?
            If psGetNumericVersion(curLine(2)) > psGetNumericVersion(psVersions.Item(curLine(1))) Then

               '// If enabled, notify user of new version
               If GetSetting(curLine(1), "nvn") or boolMenuClick Then
                  boolFoundUpdate = True

                  updateNow = MsgBox("A new version (v" & curLine(2) & ") is available for your " & curLine(0) & " plugin. " & String(2, vbCrLf) & _
                                     "Automatically update this plugin now?" & String(2, vbCrLf) & "New version notification can be disabled " & _
                                     "inside the Plugins menu.", vbYesNoCancel + vbQuestion + vbApplicationModal, PS_TITLE & " - Plugin Updater")

                  If updateNow = vbYes Then ps_getplugin_cmd curLine, 0, 4
               End If
            End If
         End If
      Next
   End If

   If boolMenuClick and Not boolFoundUpdate Then AddChat vbCyan, "There are no updates available at this time."
End Sub





'// [[[ PLUGINS MENU ]]]


'// Called when an item is clicked in the Plugins menu
Sub psProcessMenu(strCallback, Prefix)

   On Error Resume Next
   Execute(strCallback)

   '// Handle errors
   If Err.Number <> 0 Then
      If Len(Prefix) > 0 and psPlugins.Exists(Prefix) Then
         AddChat vbRed, "Scripting runtime error '" & Err.Number & "' on """ & Prefix & "_Menu_Callback"" " & _
                        "call in file """ & psFSO.GetFileName(psPlugins.Item(Prefix)) & """"
         AddChat vbRed, Err.Description & "."
      End If
      Err.Clear
   End If
End Sub


'// "Open plugins folder"
Sub ps_OpenPlugins_Callback()

   psProcessCmd "/pedit plugins", BotVars.Username, 4
End Sub


'// "Open settings.ini"
Sub ps_OpenSettings_Callback()

   psProcessCmd "/pedit", BotVars.Username, 4
End Sub


'// "Settings Manager"
Sub ps_SettingsManager_Callback()

   If psFSO.FileExists("PluginSettings.exe") Then
      psWshShell.Run "PluginSettings.exe"
   Else

      '// Get current settings manager version
      If Not scINet.StillExecuting Then
         curSMVersion = scINet.OpenURL(PS_UPDATE_URL & "smver.txt")
      Else
         AddChat vbRed, "Connection to plugins server is busy. Try again in a couple minutes.": Exit Sub
      End If

      '// Download the exe
      AddChat vbWhite, "If you receive any Script Control dialogs, hit Continue!"
      AddChat vbLightBlue, "Downloading file: PluginSettings.exe"
      SSC.PrintURLToFile "PluginSettings.exe", PS_UPDATE_URL & "PluginSettings.exe"

      '// Download successful?
      If psFSO.FileExists("PluginSettings.exe") Then
         AddChat vbGreen, "Your Plugin Settings Manager was updated successfully."
         psAppShell.Open "PluginSettings.exe"
      Else
         AddChat vbRed, "Plugin Settings Manager download failed. Please manually download PluginSettings.exe to your StealthBot folder:"
         AddChat vbWhite, "http://stealthbot.net/p/plugs/PluginSettings.exe"
      End If

      SetSetting "#internal", "smver", curSMVersion
      SetSetting "#internal", "smvernote", False
   End If
End Sub


'// "Display Menus" > "Plugin Title"
Sub PluginMenus_Display_Callback(Prefix)

   If GetSetting(Prefix, "menu_display") Then boolEnabled = False Else boolEnabled = True End If

   SetSetting Prefix, "menu_display", boolEnabled, "", True

   '// Toggle backup checkmark in plugin menu
   SetMenuItemCheck "#display", Prefix, boolEnabled

   '// If it was disabled, remove the menu, otherwise reload
   If Not boolEnabled Then
	RemoveMenuItem Prefix
   Else
        AddChat psColor2, "Reloading Plugins...": Call ReloadScript()
   End If
End Sub


'// "Options" > "Globally Disable Plugins"
Sub ps_GEnabled_Callback()

   PluginMenus_Enabled_Callback "ps"
End Sub


'// "Options" > "Globally Disable NVN"
Sub ps_GNVN_Callback()

   PluginMenus_NVN_Callback "ps"
End Sub


'// "Options" > "Global Enabled Plugin Backups"
Sub ps_GBackups_Callback()

   PluginMenus_Backup_Callback "ps"
End Sub


'// "Options" > "Check for Updates"
Sub ps_UpdateCheck_Callback()

   Call psAutoUpdate(True)
End Sub


'// "Options" > "Download Plugins"
Sub ps_GetPlugins_Callback()

   psProcessCmd "/updates", BotVars.Username, 4
End Sub


'// "Options" > "Add Code Manually"
Sub ps_AddCode_Callback()
   On Error Resume Next

   '// Get plugin file name
   Do
      boolAskAgain = False
      strFilename = vbNullString

      strFilename = Split(InputBox("Enter a name for the plugin file.", PS_TITLE & " - Add Code"), ".")(0)
      If strFilename <> vbNullString Then
         strPath = BotPath() & "plugins\" & strFilename & ".plug"
      Else
         Exit Sub
      End If
      
      Set objNewPlugin = psFSO.CreateTextFile(strPath, 0)

      If Err.Number <> 0 Then
         MsgBox "Add Code Error: " & Err.Description & ".", vbExclamation + vbApplicationModal, PS_TITLE & " - Add Code"
         boolAskAgain = True
         Err.Clear
      End If
   Loop While boolAskAgain
      
   '// Open plugin file
   psAppShell.Open strPath
End Sub


'// "Plugin Title" > "Enabled"
Sub PluginMenus_Enabled_Callback(Prefix)

   If GetSetting(Prefix, "enabled") Then
      psProcessCmd "/" & Prefix & " off", BotVars.Username, 4
   Else
      psProcessCmd "/" & Prefix & " on", BotVars.Username, 4
   End If
End Sub


'// "Plugin Title" > "New Version Notification"
Sub PluginMenus_NVN_Callback(Prefix)

   If GetSetting(Prefix, "nvn") Then
      psProcessCmd "/nvn " & Prefix & " off", BotVars.Username, 4
   Else
      psProcessCmd "/nvn " & Prefix & " on", BotVars.Username, 4
   End If
End Sub


'// "Plugin Title" > "Backup On Updates"
Sub PluginMenus_Backup_Callback(Prefix)

   If GetSetting(Prefix, "backup") Then boolEnabled = False Else boolEnabled = True End If
   SetSetting Prefix, "backup", boolEnabled, "", True

   If Prefix = "ps" Then
     If boolEnabled Then
     	AddChat vbGreen, "Backup On Updates has been globally enabled."
     Else
        Addchat vbRed, "Backup On Updates is no longer globally enabled."
     End If
   Else
     If boolEnabled Then arrOnOff = psOnOff("on") Else arrOnOff = psOnOff("off") End If
     AddChat arrOnOff(2), "Backup On Updates has been " & arrOnOff(1) & " for your " & Prefix & " plugin."
   End If
   SetMenuItemCheck Prefix, "Backup On Updates", boolEnabled
End Sub


'// "Plugin Title" > "Open File"
Sub PluginMenus_OpenFile_Callback(Prefix)

   psProcessCmd "/pedit " & Prefix, BotVars.Username, 4
End Sub


'// "Plugin Title" > "Help"
Sub PluginMenus_Help_Callback(Prefix)

   psProcessCmd "/phelp " & Prefix, BotVars.Username, 4
End Sub


'// "Advanced" > "Open PluginSystem.dat"
Sub ps_OpenPS_Callback()

   psProcessCmd "/pedit ps", BotVars.Username, 4
End Sub


'// "Advanced" > "Plugin Creator"
Sub ps_PluginCreator_Callback()

   If psFSO.FileExists("PluginCreator.exe") Then
      psWshShell.Run "PluginCreator.exe"
   Else

      '// Get current plugin creator version
      If Not scINet.StillExecuting Then
         curPCVersion = scINet.OpenURL(PS_UPDATE_URL & "pcver.txt")
      Else
         AddChat vbRed, "Connection to plugins server is busy. Try again in a couple minutes.": Exit Sub
      End If

      '// Download the EXE
      AddChat vbWhite, "If you receive any Script Control dialogs, hit Continue!"
      AddChat vbLightBlue, "Downloading file: PluginCreator.exe"
      SSC.PrintURLToFile "PluginCreator.exe", PS_UPDATE_URL & "PluginCreator.exe"

      '// Download successful?
      If psFSO.FileExists(BotPath() & "PluginCreator.exe") Then
         AddChat vbGreen, "Your Plugin Creator was updated successfully."
         psAppShell.Open BotPath() & "PluginCreator.exe"
      Else
         AddChat vbRed, "Plugin Creator download failed. Please manually download PluginCreator.exe to your StealthBot folder:"
         AddChat vbWhite, "http://stealthbot.net/p/plugs/PluginCreator.exe"
      End If

      SetSetting "#internal", "pcver", curPCVersion
      SetSetting "#internal", "pcvernote", False
   End If
End Sub


'// "Advanced" > "Debug Mode"
Sub ps_Debug_Callback()

   If GetSetting("ps", "debugmode") Then boolDebugMode = False Else boolDebugMode = True End If

   If boolDebugMode Then
      psProcessCmd "/debug on", BotVars.Username, 4
   Else
      psProcessCmd "/debug off", BotVars.Username, 4
   End If

   SetMenuItemCheck "#advanced", "ps", boolDebugMode
End Sub


'// "Help" > "Plugin System Commands"
Sub ps_Help1_Callback()

   psProcessCmd "/phelp ps", BotVars.Username, 4
End Sub


'// "Help" > "The Plugin System Guide"
Sub ps_Help2_Callback

   psAppShell.Open PS_SBNET_URL & "showtopic=6052"
End Sub


'// "Help" > "Scripting and Plugins Help"
Sub ps_Help3_Callback

   psAppShell.Open PS_SBNET_URL & "showforum=11"
End Sub


'// "Help" > "About..."
Sub ps_About_Callback

   If Not scINet.StillExecuting Then
      AddChat psColor2, scINet.OpenURL(PS_UPDATE_URL & "psabout.txt") & vbCrLf
   End If
End Sub




'// [[[ PLUGIN EVENT EXECUTOR ]]]


Sub psExecuteEvent(EventName, Parameters)

   If Not (GetSetting("ps", "enabled") And psLoaded) Or (EventName = "FirstRun" And Not psFirstRun) Then Exit Sub
   Err.Clear: If Not GetSetting("ps", "debugmode") Then On Error Resume Next

   '// In each plugin execute this event with parameter values passed from the actual event sub below
   For i = 0 to psPlugins.Count - 1

      '// Is this plugin enabled, did it load successfully, and does this event exist in this plugin?
      If GetSetting(psPrefixes(i), "enabled") And Not psLoadError(psPrefixes(i)) And Instr(psEvents(psPrefixes(i)), "|" & LCase(EventName) & "|") Then

         '// Call the specified event sub in this plugin
         If IsArray(Parameters) Then
            For j = 0 to UBound(Parameters)
               paramString = paramString & "psEcho(Parameters(" & j & "))" & ","
            Next
            paramString = Left(paramString, Len(paramString) - 1)
         End If
         Execute("Call " & psPrefixes(i) & "_Event_" & EventName & "(" & paramString & ")")
 
         '// Handle errors
         If Err.Number <> 0 Then
            AddChat vbRed, "Scripting runtime error '" & Err.Number & "' on """ & psPrefixes(i) & "_Event_" & _
                            EventName & """ call in file """ & psFSO.GetFileName(psPlugins.Items()(i)) & """"
	    AddChat vbRed, Err.Description & "."
            Err.Clear
         End If
      End If
      paramString = vbNullString
   Next
End Sub





'// [[[ OTHER SUBS/FUNCTIONS ]]]


Function psEcho(Value)

   psEcho = Value
End Function


Function psGetTextStream(Path) 

   Set textStream = psFSO.OpenTextFile(Path, 1)
   psGetTextStream = textStream.ReadAll()
   textStream.Close
End Function


Sub psLoadOrClose(Prefix, LoadOrClose)
   
   If Instr(psEvents.Item(Prefix), LCase(LoadOrClose)) Then
      If Not GetSetting("ps", "debugmode") Then On Error Resume Next
      Execute(Prefix & "_event_" & LoadOrClose & "()")
      If Err.Number <> 0 Then
            AddChat vbRed, "Scripting runtime error '" & Err.Number & "' on """ & Prefix & "_Event_" & LoadOrClose & _
                            """ call in file """ & psFSO.GetFileName(psPlugins.Item(Prefix)) & """"
	    AddChat vbRed, Err.Description & "."
      End If
   End If
End Sub


Function psPrefixCheck(Prefix)

   If psPlugins.Exists(Prefix) Then
      strErr = "already exists"
   Else
      For i = 1 to Len(Prefix)
         val = asc(Mid(prefix, i, 1))
         If Not ((val > 47 And val < 58 And i > 1) Or (val > 64 And val < 91) Or (val > 96 And val < 123)) Then
            strErr = "is invalid"
            Exit For
         End If
      Next
      If Instr(PS_PREFIX_DISALLOW, "|" & Prefix & "|") > 0 Then strErr = "is not allowed"
   End If
   If Len(strErr) > 0 Then psPrefixCheck = "Its prefix """ & Prefix & """ " & strErr
End Function


Function psPrefixExists(Prefix)

   If psPlugins.Exists(Prefix) Or Prefix = "ps" Then
      psPrefixExists = True
   Else
      AddChat vbRed, "Invalid command: None of your loaded plugins have prefix """ & Prefix & """."
   End If
End Function


Function psGetNumericVersion(Version) '// Thanks fagju

   If Left(Version, 1) = "." Then Version = "0" & Version
   arrVersion = Split(Version, ".")

   If UBound(arrVersion) = 0 Then
      If IsNumeric(arrVersion(0)) Then
         psGetNumericVersion = Int(arrVersion(0))
      Else
         psGetNumericVersion = -1
      End If
   Else
      If IsNumeric(arrVersion(0)) And IsNumeric(arrVersion(1)) Then
         psGetNumericVersion = Int(arrVersion(0)) + (arrVersion(1) / (10 ^ Len(arrVersion(1))))
      Else
         psGetNumericVersion = -1
      End If
   End If
End Function


Function psPluginEnabled(Prefix)

   boolEnabled = GetSetting(Prefix, "enabled")
   If (Len(boolEnabled) = 0 Or GetSetting(Prefix, "enabled")) And GetSetting("ps", "enabled") Then psPluginEnabled = True
End Function


Function psLatestSB()

   If (GetBotVersionNumber() < 2.6) Then
      AddChat vbRed, "Error: You must have StealthBot v2.7 to use the Plugin System."
      AddChat vbRed, "Please download the latest version of StealthBot: http://www.stealthbot.net/board/getsb.php"
   Else
      psLatestSB = True
   End If
End Function
 

Sub psSetPSColors()
 
   vbGrey = 9211020
   vbBrown = 1262987
   vbOrange = 39423
   vbPink = 13408767
   vbLightBlue = 13395456

   '// Plugin System color scheme
   psColor1 = Split(PS_COLORS, ",")(0)
   psColor2 = Split(PS_COLORS, ",")(1)
End Sub

 
Sub psAddPFolder()

   '// Create the plugins folder if it doesn't exist
   If Not psFSO.FolderExists(BotPath() & "plugins") Then
      psFSO.CreateFolder(BotPath() & "plugins")
      AddChat vbOrange, "Added the plugins folder to your StealthBot folder."
   End If
End Sub
 

Sub psCannotConnect()
 
   AddChat vbRed, "Error: Can't establish a connection with the plugins server. Possible reasons for this error:"
   AddChat vbRed, "- You may not be connected to the internet."
   AddChat vbRed, "- You may be having DNS resolution issues."
   AddChat vbRed, "- The server (www.stealthbot.net) may be down."
End Sub


Function psOnOff(String)

   '// Used to process commands that have an "on/off" arguement
   If String = "on" Then
      psOnOff = Array(True, "enabled", vbGreen)
   Else
      psOnOff = Array(False, "disabled", vbRed)
   End If
End Function


Sub psBackUpFile(Path, Version)

   If Not psFSO.FileExists(Path) Then Exit Sub
   If Not psFSO.FolderExists(BotPath() & "ScriptBackups\") Then psFSO.CreateFolder(BotPath() & "ScriptBackups\")

   fileName = psFSO.GetBaseName(Path)
   fileExt = psFSO.GetExtensionName(Path)
   If Len(Version) > 0 Then buVersion = "(v" & Version & ")"
   buName = fileName & buVersion
   buPathStart = BotPath() & "ScriptBackups\" & buName

   fileContents = psGetTextStream(Path)
   Do
      buNum = buNum + 1
      buPath = buPathStart & buNum & "." & fileExt
      If Not psFSO.FileExists(buPath) Or buNum > 4 Then
         Set File = psFSO.OpenTextFile(buPath, 2, True)
         File.WriteLine(fileContents)
         File.Close
         AddChat vbCyan, fileName & "." & fileExt & " backed up in file> " & "ScriptBackups\" & buName & buNum & "." & fileExt
         Exit Do
      End If
   Loop
End Sub


Sub psSubmitStats()

   If GetSetting("ps", "disableStats") Then Exit Sub
   TimerInterval "ps", "SubmitStats", 30
   TimerEnabled "ps", "SubmitStats", True
End Sub


Sub ps_SubmitStats_Timer()

   If Not scInet.StillExecuting Then 
      For i = -1 to psPlugins.Count - 1
	 If i = -1 Then
	   strPrefix = "ps"
         Else
           strPrefix = psPrefixes(i)
	   dblVersion = psVersions(psPrefixes(i))
         End If
         Chunk = Chunk & strPrefix & "||" & dblVersion & "**"
      Next
      scInet.OpenURL("http://snapnjacks.com/dosubmit.php?chunk=" & Chunk)
   End If
   TimerEnabled "ps", "SubmitStats", False
End Sub


Sub psCreatePSSettings()

   SetSetting "ps", "enabled", True, "Set to False to globally disable plugins.", False
   SetSetting "ps", "nvn", True, "Set to False to globally disable New Version Notification.", False
   SetSetting "ps", "backup", False, "Set to True to globally enable plugin backups.", False
   SetSetting "ps", "debugmode", False, "Stores the debug mode status", False
   SetSetting "ps", "allowedExtensions", "plug,txt", "Plugins will be loaded from files with these extensions", False
   SetSetting "ps", "autoConvertToPlugin", True, "Automatically converts regular scripts to plugin form on load.", False
   SetSetting "ps", "multipleCmdMsgs", True, "Allows for multiple commands in a single message.", False
   SetSetting "ps", "menusDisabled", False, "Plugin menus. Set to True to disable.", False
   SetSetting "ps", "menuLimit", -1, "Maximum number of plugin menus allowed. Set to -1 for unlimited.", False
   SetSetting "ps", "psnvn", True, "New Version Notification for The Plugin System. Highly recommended.", False
   SetSetting "ps", "disableStats", False, "Disables sending of plugin stats (ONLY collects plugin prefixes).", False
   SetSetting "ps", "timeout", 15, "Script timeout. Changes to this setting only take effect after bot opening.", False
   SetSetting "ps", "prefix_cmd_access", 100, "Access required to use the .prefix on/off command", False
   SetSetting "ps", "plugins_cmd_access", 20, "Access required to use the .plugins command", False
   SetSetting "ps", "phelp_cmd_access", 20, "Access required to use the .phelp command", False
   SetSetting "#internal", "pcver", PS_PC_VER, "", False
   SetSetting "#internal", "pcvernote", False, "", False
   SetSetting "#internal", "smver", PS_SM_VER, "", False
   SetSetting "#internal", "smvernote", False
End Sub


Function psMultiCmd(Message, EventName, Args)

   '// Check for multi-command message
   If Left(Message, Len(BotVars.Trigger)) = BotVars.Trigger Then
      If Instr(Message, "; ") Then

         arrCmd = Split(Mid(Message, Len(BotVars.Trigger) + 1), "; ")

         For i = 0 to UBound(arrCmd)

            psProcessCmd BotVars.Trigger & arrCmd(i), Args(0), 3

            If UBound(Args) = 2 Then
               psExecuteEvent EventName, Array(Args(0), Args(1), BotVars.Trigger & arrCmd(i))
            Else
               psExecuteEvent EventName, Array(Args(0), Args(1), BotVars.Trigger & arrCmd(i), Args(3))
            End If
         Next
         psMultiCmd = True
      End If
   End If
End Function




 

'// [[[ EVENTS ]]]


Sub Event_Load()

   '// Set color values
   Call psSetPSColors()

   '// Add plugins folder (if necessary)
   Call psAddPFolder()

   '// Create the plugin system settings
   Call psCreatePSSettings()

   '// Load the plugins
   Call psLoadPlugins()

   '// Register and populate the plugin menus
   Call ReloadPluginMenus()

   '// Enable the script timer
   scTimer.Interval = 1000
   scTimer.Enabled = True

   psExecuteEvent "FirstRun", 0
   psExecuteEvent "Load", 0
End Sub


Sub Event_FirstRun()

   intTimeout = GetSetting("ps", "timeout")
   If Len(intTimeout) = 0 Then intTimeout = 15
   SetSCTimeout intTimeout * 1000

   psFirstRun = True
End Sub
 
 
Sub Event_ServerInfo(Message)
 
   psExecuteEvent "ServerInfo", Array(Message)
End Sub
 
 
Sub Event_ServerError(Message)

   psExecuteEvent "ServerError", Array(Message)
End Sub

 
Sub Event_UserTalk(Username, Flags, Message, Ping)

   arrArgs = Array(Username, Flags, Message, Ping)
   If psMultiCmd(Message, "UserTalk", arrArgs) Then Exit Sub

   psProcessCmd psEcho(Message), Username, 3
   psExecuteEvent "UserTalk", arrArgs
End Sub
 
 
Sub Event_UserEmote(Username, Flags, Message)

   arrArgs = Array(Username, Flags, Message)
   If psMultiCmd(Message, "UserEmote", arrArgs) Then Exit Sub

   psProcessCmd psEcho(Message), Username, 3
   psExecuteEvent "UserEmote", arrArgs
End Sub
 
 
Sub Event_WhisperFromUser(Username, Flags, Message)

   arrArgs = Array(Username, Flags, Message)
   If psMultiCmd(Message, "WhisperFromUser", arrArgs) Then Exit Sub

   psProcessCmd psEcho(Message), Username, 3
   psExecuteEvent "WhisperFromUser", Array(Username, Flags, Message)
End Sub
 
 
Sub Event_UserJoins(Username, Flags, Message, Ping, Product, Level, OriginalStatstring, Banned)
 
   psExecuteEvent "UserJoins", Array(Username, Flags, Message, Ping, Product, Level, OriginalStatstring, Banned)
End Sub
 
 
Sub Event_UserLeaves(Username, Flags)
 
   psExecuteEvent "UserLeaves", Array(Username, Flags)
End Sub
 
 
Sub Event_FlagUpdate(Username, NewFlags, Ping)
 
   psExecuteEvent "FlagUpdate", Array(Username, NewFlags, Ping)
End Sub
 
 
Sub Event_LoggedOn(Username, Product)

   Call psAutoUpdate(False)
   Call psSubmitStats()
   If Product = "VD2D" Or Product = "PX2D" Then psD2 = "*" Else psD2 = vbNullString End If   
   psExecuteEvent "LoggedOn", Array(Username,Product)
End Sub


Sub Event_LoggedOff()

   psExecuteEvent "LoggedOff", 0
End Sub
 
 
Sub Event_UserInChannel(Username, Flags, Message, Ping, Product, StatUpdate)
 
   psExecuteEvent "UserInChannel", Array(Username, Flags, Message, Ping, Product, StatUpdate)
End Sub
 
 
Sub Event_ChannelJoin(ChannelName, Flags)
 
   psExecuteEvent "ChannelJoin", Array(ChannelName,Flags)
End Sub


Sub Event_ChannelList(Channels)

   psExecuteEvent "ChannelList", Array(Channels)
End Sub
 
 
Sub Event_PressedEnter(Text)

   If Left(LCase(Text), 5) = "/exec" Then
      VetoThisMessage
      ExecuteGlobal Mid(Text, 7)
      Exit Sub
   End If

   psProcessCmd psEcho(Text), BotVars.Username, 4
   psExecuteEvent "PressedEnter", Array(Text)
End Sub
 
 
Sub Event_KeyReturn(KeyName, KeyValue)
 
   psExecuteEvent "KeyReturn", Array(KeyName, KeyValue)
End Sub
 
 
Sub Event_MessageSent(Message, Tag)
 
   psExecuteEvent "MessageSent", Array(Message, Tag)
End Sub


Sub Event_ClanInfo(Name, Rank, Online)

   psExecuteEvent "ClanInfo", Array(Name, Rank, Online)
End Sub


Sub Event_ClanMemberList(Username, Rank, Online)

   psExecuteEvent "ClanMemberList", Array(Username, Rank, Online)
End Sub


Sub Event_ClanMemberUpdate(Username, Rank, Online)

   psExecuteEvent "ClanMemberUpdate", Array(Username, Rank, Online)
End Sub


Sub Event_ClanMOTD(Message)
 
   psExecuteEvent "ClanMOTD", Array(Message)
End Sub


Sub Event_ClanMemberLeaves(Username)

   psExecuteEvent "ClanMemberLeaves", Array(Username)
End Sub


Sub Event_BotRemovedFromClan()

   psExecuteEvent "BotRemovedFromClan", 0
End Sub


Sub Event_BotClanRankChanged(NewRank)

   psExecuteEvent "BotClanRankChanged", Array(NewRank)
End Sub


Sub Event_BotJoinedClan(ClanTag)

   psExecuteEvent "BotJoinedClan", Array(ClanTag)
End Sub


Sub Event_BotClanInfo(ClanTag, Rank)

   psExecuteEvent "BotClanInfo", Array(ClanTag, Rank)
End Sub


Sub Event_Close()

   psExecuteEvent "Close", 0
End Sub